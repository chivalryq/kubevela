apiVersion: core.oam.dev/v1beta1
kind: ComponentDefinition
metadata:
  name: helm
  namespace: vela-system
  annotations:
    definition.oam.dev/description: "helm release is a group of K8s resources from either git repository or helm repo"
spec:
  workload:
    type: autodetects.core.oam.dev
  schematic:
    cue:
      template: |
        output: {
        	apiVersion: "source.toolkit.fluxcd.io/v1beta1"
        	metadata: {
        		name: context.name
        	}
        	if parameter.git != _|_ {
        		kind: "GitRepository"
        		spec: {
        			interval: parameter.pullInterval
        			url:      parameter.url
        			ref:
        				branch: parameter.git.branch
        		}
        	}
        	if parameter.helm != _|_ {
        		kind: "HelmRepository"
        		spec: {
        			interval: parameter.pullInterval
        			url:      parameter.url
        			if parameter.helm.secretRef != _|_ {
        				secretRef: {
        					name: parameter.helm.secretRef
        				}
        			}
        		}
        	}
        	if parameter.oss != _|_ {
        		kind: "Bucket"
        		spec: {
        			endpoint:   parameter.url
        			interval:   parameter.pullInterval
        			bucketName: parameter.oss.bucketName
        			provider:   parameter.oss.provider
        			if parameter.oss.region != _|_ {
        				region: parameter.oss.region
        			}
        			if parameter.oss.timeout != _|_ {
        				timeout: parameter.oss.timeout
        			}
        			if parameter.oss.secretRef != _|_ {
        				secretRef: {
        					name: parameter.oss.secretRef
        				}
        			}
        		}
        	}
        }

        outputs: release: {
        	apiVersion: "helm.toolkit.fluxcd.io/v2beta1"
        	kind:       "HelmRelease"
        	metadata: {
        		name: context.name
        	}
        	spec: {
        		interval: parameter.pullInterval
        		chart: {
        			spec: {
        				chart:   parameter.chart
        				version: parameter.version
        				sourceRef: {
        					if parameter.git != _|_ {
        						kind: "GitRepository"
        					}
        					if parameter.helm != _|_ {
        						kind: "HelmRepository"
        					}
        					if parameter.oss != _|_ {
        						kind: "Bucket"
        					}
        					name:      context.name
        					namespace: context.namespace
        				}
        				interval: parameter.pullInterval
        			}
        		}
        		if parameter.targetNamespace != _|_ {
        			targetNamespace: parameter.targetNamespace
        		}
        		if parameter.releaseName != _|_ {
        			releaseName: parameter.releaseName
        		}
        		if parameter.values != _|_ {
        			values: parameter.values
        		}
        	}
        }

        parameter: {
        	// +usage=The interval at which to check for repository/bucket and relese updates, default to 5m
        	pullInterval: *"5m" | string
        	// +usage=The Git or Helm repository URL, OSS endpoint, accept HTTP/S or SSH address as git url,
        	url: string

        	git?: {
        		// +usage=The Git reference to checkout and monitor for changes, defaults to master branch
        		branch: *"master" | string
        	}
        	helm?: {
        		// +usage=The name of the secret containing authentication credentials for Helm repository or oss bucket
        		secretRef?: string
        	}
        	oss?: {
        		// +usage=The bucket's name, required if repoType is oss
        		bucketName: string
        		// +usage="generic" for Minio, Amazon S3, Google Cloud Storage, Alibaba Cloud OSS, "aws" for retrieve credentials from the EC2 service when credentials not specified, default "generic"
        		provider: *"generic" | "aws"
        		// +usage=The bucket region, optional
        		region?: string
        		// +usage=The timeout for download operations, optional
        		timeout?: string
        		// +usage=The name of the secret containing authentication credentials for Helm repository or oss bucket
        		secrietRef?: string
        	}

        	// +usage=1.The relative path to helm chart for git/oss source. 2. chart name for helm resource 3. relative path for chart package(e.g. ./charts/podinfo-1.2.3.tgz)
        	chart: string
        	// +usage=Chart version
        	version: *"*" | string
        	// +usage=The namespace for helm chart, optional
        	targetNamespace?: string
        	// +usage=The release name
        	releaseName?: string
        	// +usage=Chart values
        	values?: #nestedmap
        }

        #nestedmap: {
        	...
        }